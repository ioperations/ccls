# Maintainer: aodong qin <ioperations.c at gmail.com>
## makepkg -o PKGBUILD
## pacman -U ./ccls*

pkgname=ccls
_pkgname=ccls
pkgver=20210907
pkgrel=1
pkgdesc='C/C++ language server supporting cross references, hierarchies, completion and semantic highlighting'
arch=('x86_64')
url='https://github.com/ioperations/ccls'
license=('Apache')
depends=('clang' 'llvm-libs')
makedepends=("cmake" "git" "llvm")
source=('git+https://github.com/ioperations/ccls.git')
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  TZ=UTC date -d @$(git log -1 --format=%ct) +%Y%m%d
}

prepare() {
  cd $_pkgname
  git submodule update --init
}

build() {
  cd $_pkgname
  cmake -H. -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_COMPILER=clang++ -DCLANG_LINK_CLANG_DYLIB=on -DUSE_SYSTEM_RAPIDJSON=off
  cmake --build build
}

package() {
  cd $_pkgname/build
  make DESTDIR="$pkgdir" install
}
